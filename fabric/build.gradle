plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'

    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}
base {
    archivesName = "${mod_id}-fabric"
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")

    implementation group: 'org.kohsuke.metainf-services', name: 'metainf-services', version: '1.9'
    annotationProcessor group: 'org.kohsuke.metainf-services', name: 'metainf-services', version: '1.9'
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "${mod_id}-${project.name}"
            from components.java
        }
    }
    repositories {
        maven {
            name = "BrazierModdingRepository"
            url = uri("https://maven.brazier-modding.net/snapshots")
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

curseforge{
    apiKey = project.findProperty('curse_auth')
    project{
        id = project.findProperty('curse_project')
        releaseType = project.findProperty('curse_release_type_fabric')
        changelogType = 'markdown'
        if(new File("../", "Changelog.md").exists())
            changelog = new File("../", "Changelog.md").text

        Arrays.asList(project.findProperty('curse_versions').toString().split(",")).forEach(version -> addGameVersion(version));
        addGameVersion 'Fabric'

        mainArtifact(remapJar) {
            displayName = "$project.ext.mod_name - Fabric - $project.ext.minecraft_version - v$project.simple_version"
        }

        afterEvaluate{
            uploadTask.dependsOn(remapJar)
        }
        options{
            forgeGradleIntegration = false;
        }
    }
}

modrinth {
    token = project.findProperty('modrinth_auth')
    projectId = project.findProperty('modrinth_project')
    versionType = project.findProperty('modrinth_release_type_fabric')
    versionName = "$project.ext.mod_name - v${simple_version}"
    version = "${version}_fabric"
    uploadFile = remapJar
    gameVersions = Arrays.asList(project.findProperty('modrinth_versions').toString().split(","))
    loaders = ["fabric"]

    if(new File("../", "Changelog.md").exists())
        changelog = new File("../", "Changelog.md").text
}

